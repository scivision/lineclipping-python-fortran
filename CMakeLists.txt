cmake_minimum_required(VERSION 3.7)
project(lineclip Fortran)
enable_testing()

add_compile_options(-O3 -g)

if(${CMAKE_Fortran_COMPILER_ID} STREQUAL GNU)
  if(${CMAKE_Fortran_COMPILER_VERSION} VERSION_GREATER_EQUAL 8.1)
    list(APPEND FFLAGS -std=f2018)
  else()
    list(APPEND FFLAGS -std=f2008ts)
  endif()
  
  list(APPEND FFLAGS -mtune=native -Wall -Wextra -Wpedantic -Werror=array-bounds -fbacktrace -fexceptions)
  if(NOT CYGWIN AND NOT WIN32)
    list(APPEND FFLAGS -fstack-protector-all)
  endif()
elseif(${CMAKE_Fortran_COMPILER_ID} STREQUAL Flang)
  link_libraries(-static-flang-libs)
endif()

# TODO should be able to run coarrays demo even in single case, may be algorithmic error in my Coarray demo code?
set(DEMO_COARRAYS FALSE)
if(${CMAKE_Fortran_COMPILER_ID} STREQUAL Intel)
    add_compile_options(-coarray=shared)# -check all)
    link_libraries(-coarray=shared)
    set(DEMO_COARRAYS TRUE)
elseif(${CMAKE_Fortran_COMPILER_ID} STREQUAL GNU)
  find_package(OpenCoarrays)
  
  if(OpenCoarrays_FOUND)
    list(APPEND OpenCoarrays_LIBRARIES caf_mpi)
    list(APPEND FFLAGS -fcoarray=lib)
    set(DEMO_COARRAYS TRUE)
  else()
    list(APPEND FFLAGS -fcoarray=single)
  endif()
endif()

add_library(lineclip lineclipping.f90)

add_library(assert assert.f90)

add_executable(RunLineclip DemoLineclip.f90)
target_compile_options(RunLineclip PRIVATE ${FFLAGS})
target_link_libraries(RunLineclip PRIVATE assert lineclip ${OpenCoarrays_LIBRARIES})
add_test(NAME clip COMMAND RunLineclip)

if(DEMO_COARRAYS)
  add_executable(CoarrayLineclip DemoCoarrayLineclip.f90)
  target_compile_options(CoarrayLineclip PRIVATE ${FFLAGS})
  target_link_libraries(CoarrayLineclip PRIVATE assert lineclip ${OpenCoarrays_LIBRARIES})
  add_test(NAME CoarrayClip COMMAND cafrun CoarrayLineclip)
endif()
